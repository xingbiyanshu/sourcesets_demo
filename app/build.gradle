apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.sissi.sourcesetsdemo"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "mode"
    productFlavors {
        demo {
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
            dimension "mode"
        }
        full {
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            dimension "mode"
        }
        keda {
            applicationIdSuffix ".keda"
            versionNameSuffix "-keda"
            dimension "mode"
        }
    }

    productFlavors.all { flavor ->
        logger.quiet("flavor.name=$flavor.name")

        if (flavor.name != "keda") {
            sourceSets[flavor.name].java.srcDir 'src/common_factor_2/java'
//            sourceSets[flavor.name].res.srcDir 'src/common_factor_2/res' // srcDir是追加的方式,在默认的路径基础上追加该路径
//            sourceSets[flavor.name].manifest.srcFile 'src/common_factor_2/AndroidManifest.xml' //manifest是排他的,此处设定会覆盖默认的路径
        }

        if (flavor.name != "demo") {
            sourceSets[flavor.name].java.srcDir 'src/common_factor_1/java'
            sourceSets[flavor.name].res.srcDir 'src/common_factor_1/res' // srcDir是追加的方式,在默认的路径基础上追加该路径
            sourceSets[flavor.name].manifest.srcFile 'src/common_factor_1/AndroidManifest.xml' //manifest是排他的,此处设定会覆盖默认的路径
        }

        // DEBUG
        sourceSets[flavor.name].java.srcDirs.each { srcDir ->
            logger.quiet("java.srcDir.path=$srcDir.path")
        }
        sourceSets[flavor.name].res.srcDirs.each { srcDir ->
            logger.quiet("res.srcDir.path=$srcDir.path")
        }
        logger.quiet("manifest.path=" + sourceSets[flavor.name].manifest.srcFile.path)
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
}
